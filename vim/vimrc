" ------------------------------------------------------------------------------
" # Defaults
" ------------------------------------------------------------------------------

" Vim 8 defaults
" For more info, see :help defaults.vim
unlet! skip_defaults_vim
silent! source $VIMRUNTIME/defaults.vim


" ------------------------------------------------------------------------------
" # Sourcing
" ------------------------------------------------------------------------------

" Dotfiles path
let g:dotfiles_path = '/Users/jesseleite/.dotfiles'

" Get path relative to vimrc
function! VimrcPath(path)
  return g:dotfiles_path . '/vim/' . a:path
endfunction

" Vimrc related paths
let g:vimrc_related_paths = [
  \ $MYVIMRC,
  \ VimrcPath('vimrc'),
  \ VimrcPath('*.vim')
  \ ]

" Source plugins
call plug#begin('~/.vim/plugged')
  execute 'source ' . VimrcPath('plugins.vim')
call plug#end()

" Can this be included via vim-plug?
packadd! matchit

" Source mappings
execute 'source ' VimrcPath('mappings.vim')

" Setup dynamic sourcing
function! s:SourceConfigFilesIn(folder)
  for config_file in split(glob(VimrcPath(a:folder . '/*')), '\n')
    if filereadable(config_file)
      execute 'source' config_file
    endif
  endfor
endfunction

" Dynamic sourcing
call s:SourceConfigFilesIn('local-config')
call s:SourceConfigFilesIn('plugin-config')

" Auto sourcing
augroup autosource_vimrc
  autocmd!
  execute 'autocmd BufWritePost ' . join(g:vimrc_related_paths, ',') . ' nested source ' . $MYVIMRC
augroup END


" ------------------------------------------------------------------------------
" # Still organizing...
" ------------------------------------------------------------------------------

set hidden
set nobackup
set noswapfile
set autoread
set confirm
set encoding=utf-8
set clipboard=unnamed
set backspace=indent,eol,start
set relativenumber
set noshowmode
" set lazyredraw                               " Had this for some reason, but it causes deoplete flicker
set updatetime=1000                            " Set updatetime for CursorHold, gitgutter, etc.
set fillchars+=vert:\ ,                        " Vertical split character
call matchadd('ColorColumn', '\%121v', 100)    " Only show 121st character on lines that might exceed 120
set completeopt=menu,menuone,noinsert,noselect " Auto complete menu options

" Always insert completion popup candidate without entering new line.
inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<CR>"

augroup misc_commands
  autocmd!
  autocmd FocusGained,BufEnter,CursorHold,CursorHoldI * checktime
  autocmd BufEnter * EnableStripWhitespaceOnSave
augroup END

